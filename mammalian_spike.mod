TITLE HH style channels for spiking retinal ganglion cells
:
: Modified from Fohlmeister et al, 1990, Brain Res 510, 343-345
: by TJ Velte March 17, 1995
: must be used with calcium pump mechanism, i.e. capump.mod
:
: Modified to update the procedure block to a derivative block
: by Kate Finn March 12, 2020
:
: Modified to follow the mammalian channel properties at 37.1 deg C
: from Fohlmeister et al, 2010, J Neurophysiology 103, 1357-1354
:

NEURON {
	SUFFIX mammalian_spike
	USEION na READ ena WRITE ina
	USEION k READ ek WRITE ik
	USEION ca READ cai, eca, cao WRITE ica
	RANGE gnabar, gkbar, gcabar, gkcbar
	RANGE m_inf, h_inf, n_inf, c_inf
	RANGE tau_m, tau_h, tau_n, tau_c
    RANGE idrk, icak, ina
}


UNITS {
	(molar) = (1/liter)
	(mM) = (millimolar)
	(mA) = (milliamp)
	(mV) = (millivolt)
}

PARAMETER {
	: These conductances are from the axon - they are overwritten in the HOC file for other regions
	gnabar	= 0.100	(mho/cm2)
	gkbar	= 0.050 (mho/cm2)
	gcabar	= 0.00075	(mho/cm2)
	gkcbar	= 0.0002 (mho/cm2)
	g_pas   = 0.0001
	: Equilibrium potentials
	ena 	= 61.02	(mV)
	ek  	= -102.03	(mV)
	eca     (mV)
	e_pas   = -65.02 (mV)
	: Calcium ion concentration
	cao	= 1.8	(mM)
	cai     = 0.0001 (mM)
}

STATE {
	m h n c 
}

INITIAL {
: The initial values were determined at a resting value of -65.02 mV 
    m = 0.0353
    h = 0.9054
    n = 0.0677
    c = 0.0019
}

ASSIGNED {
	v (mV)
	celsius (degC)
	ina	(mA/cm2)
	ik	(mA/cm2)
	    idrk    (mA/cm2)
	    icak    (mA/cm2)
	ica	(mA/cm2)
	m_inf h_inf n_inf c_inf
	tau_m tau_h tau_n tau_c
}

BREAKPOINT {
	SOLVE states METHOD cnexp
	ina = gnabar * m*m*m*h * (v - ena)
        idrk = gkbar * n*n*n*n * (v - ek)
        icak = gkcbar * ((cai / 0.001)/ (1 + (cai / 0.001))) * (v - ek)
        ik = idrk + icak
	ica = gcabar * c*c*c * (v - eca)
}

DERIVATIVE states {	
	trates(v)
	m' = (m_inf - m)/tau_m
	h' = (h_inf - h)/tau_h
	n' = (n_inf - n)/tau_n
	c' = (c_inf - c)/tau_c
}


PROCEDURE trates(vm) { 
	LOCAL a,b
	
:NA m
	a = (-3.136 * (v+35)) / ((exp(-0.1*(v+35))) - 1)
	b = 104.545 * (exp((-1*(v+60))/20))
	tau_m = 1 / (a + b)
	m_inf = a * tau_m

:NA h
	a = 2.091 * (exp((-1*(v+52))/20))
	b = 31.365 / ( 1 + exp(-0.1 *(v+22)))
	tau_h = 1 / (a + b)
	h_inf = a * tau_h

:K n (non-inactivating, delayed rectifier)
	a = (-0.110 * (v+37)) / ((exp(-0.1*(v+37))) - 1)
	b = 2.191 * (exp((-1*(v + 47))/80))
	tau_n = 1 / (a + b)
	n_inf = a * tau_n

:CA channel
	a = (-1.568 * (v+13)) / ((exp(-0.1*(v+13))) - 1)
	b = 52.267 * (exp((-1*(v + 38))/18))
	tau_c = 1 / (a + b)
	c_inf = a * tau_c

}
